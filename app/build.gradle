import groovy.json.JsonOutput

plugins {
    id("com.android.application")
    id("org.jetbrains.kotlin.android")
    id("com.google.devtools.ksp")
}

def isRelease = false

def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdk = 31

    defaultConfig {
        applicationId "com.yikwing.ykquickdev"
        minSdk = 24
        targetSdk = 31
        versionCode = 1
        versionName = "1.0"

        testInstrumentationRunner = "androidx.test.runner.AndroidJUnitRunner"

        ksp {
            arg("room.schemaLocation", "$projectDir/schemas".toString())
        }
    }

    signingConfigs {
        config {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            signingConfig signingConfigs.config
            buildConfigField "String", "YK_CONFIG", "\"" + JsonOutput.toJson(rootProject.ext.injectEnv).replaceAll("\"", "\\\\\"") + "\""
        }
        release {
            // 启用代码压缩、优化及混淆
            minifyEnabled true
            // 启用资源压缩
            shrinkResources true
            signingConfig signingConfigs.config
            buildConfigField "String", "YK_CONFIG", "\"" + JsonOutput.toJson(rootProject.ext.injectEnv).replaceAll("\"", "\\\\\"") + "\""
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    defaultConfig {
        ndk {
            abiFilters 'arm64-v8a'
        }
    }

    lintOptions {
        checkDependencies true
    }

}

dependencies {
    implementation("androidx.core:core-ktx:${rootProject.ext.coreKtxVersion}")
    implementation("androidx.appcompat:appcompat:${rootProject.ext.appCompatVersion}")
    implementation("com.google.android.material:material:${rootProject.ext.materialVersiob}")
    implementation("androidx.constraintlayout:constraintlayout:${rootProject.ext.constraintlayoutVersion}")
    implementation("androidx.lifecycle:lifecycle-runtime-ktx:${rootProject.ext.lifecycleRuntimeKtxVersion}")

    implementation("androidx.room:room-runtime:${rootProject.ext.roomVersion}")
    ksp("androidx.room:room-compiler:${rootProject.ext.roomVersion}")
    implementation("androidx.room:room-ktx:${rootProject.ext.roomVersion}")

    implementation("com.squareup.moshi:moshi:${rootProject.ext.moshiVersion}")
    ksp("com.squareup.moshi:moshi-kotlin-codegen:${rootProject.ext.moshiVersion}")

    implementation("org.jetbrains.kotlinx:kotlinx-coroutines-android:${rootProject.ext.coroutinesVersion}")
    implementation("androidx.fragment:fragment-ktx:${rootProject.ext.fragmentKtxVersion}")


    if (isRelease) {
        implementation("com.github.yikwing.ykmagic:yk-network:0.0.5")
        implementation("com.github.yikwing.ykmagic:yk-config:0.0.5")
        implementation("com.github.yikwing.ykmagic:yk-proxy:0.0.5")
    } else {
        implementation project(path: ':module_config')
        implementation project(path: ':module_network')
        implementation project(path: ':module_proxy')
    }

    implementation("io.coil-kt:coil:${rootProject.ext.coilVersion}")

    debugImplementation("com.guolindev.glance:glance:${rootProject.ext.glanceVersion}")

    testImplementation("junit:junit:${rootProject.ext.junit}")
    androidTestImplementation("androidx.test.ext:junit:1.1.3")
    androidTestImplementation("androidx.test.espresso:espresso-core:3.4.0")

}